10 CLS : PRINT "MiniBASIC test program." : PRINT 
20 PRINT "This program automatically tests as many MiniBASIC commands as"
30 PRINT "possible.  Any BREAK below represents an unexpected failure."
40 PRINT : PRINT "Press any key to begin."
50 GET A$ : PRINT 
100 REM Numeric Functions
110 PRINT "Testing numeric functions..."
120 IF ABS(-42) <> 42 THEN BREAK 
130 IF ASC("A") <> 65 OR ASC("a") <> 97 THEN BREAK 
140 IF ABS(ATN(1) - 0.785398) > 0.0001 THEN BREAK 
150 IF COS(PI) <> -1 OR COS(0) <> 1 THEN BREAK 
160 IF EXP(0) <> 1 OR ABS(EXP(1) - 2.718282) > 0.0001 THEN BREAK 
170 IF FIX(4.8) <> 4 OR FIX(-4.8) <> -4 THEN BREAK 
180 IF INT(4.8) <> 4 OR INT(-4.8) <> -5 THEN BREAK 
190 IF INSTR("abcde", "cd") <> 3 THEN BREAK 
200 IF INSTR("abcde", "cq") <> 0 THEN BREAK 
210 IF INSTR(4, "abcdecde", "cd") <> 6 THEN BREAK 
220 IF LEN("abcde") <> 5 THEN BREAK 
230 IF POS(0) <> 1 THEN BREAK 
240 IF POS(1) <> 9 THEN BREAK 
250 R = RND(0) : IF R < 0 OR R > 1 THEN BREAK 
260 X = RND(-42) : 
270 IF ABS(RND(0) - 0.125518) > 0.0001 THEN BREAK 
280 IF ABS(RND(0) - 0.522764) > 0.0001 THEN BREAK 
290 IF ABS(RND(0) - 0.168434) > 0.0001 THEN BREAK 
300 IF SGN(-42) <> -1 OR SGN(0) <> 0 OR SGN(42) <> 1 THEN BREAK 
310 IF ABS(SIN(PI)) > 0.0001 OR SIN(PI / 2) <> 1 THEN BREAK 
320 IF SQR(9) <> 3 OR SQR(25) <> 5 THEN BREAK 
330 IF ABS(TAN(PI / 4) - 1) > 0.0001 OR ABS(TAN(PI)) > 0.0001 THEN BREAK 
340 IF TIME(1) < 0 OR TIME(1) > TIME(0) THEN BREAK 
350 IF VAL("-40.2") <> -40.2 THEN BREAK 
999 PRINT "Numeric functions OK."
1000 REM String Functions
1010 PRINT "Testing string functions..."
1020 IF CHR$(65) <> "A" OR CHR$(97) <> "a" THEN BREAK 
1030 IF LEFT$("abcde", 3) <> "abc" THEN BREAK 
1040 IF LOWER$("AbCdE") <> "abcde" THEN BREAK 
1050 IF MID$("abcde", 3) <> "cde" THEN BREAK 
1052 IF MID$("abcde", 3, 2) <> "cd" THEN BREAK 
1060 IF RIGHT$("abcde", 3) <> "cde" THEN BREAK 
1070 IF SPACE$(5) <> "     " THEN BREAK 
1080 IF STR$(-40.2) <> "-40.2" THEN BREAK 
1090 IF UPPER$("AbCdE") <> "ABCDE" THEN BREAK 
1999 PRINT "String functions OK."
2000 REM General Commands
2010 PRINT "Testing general BASIC commands..."
2020 X = 42 : CLEAR : IF X <> 0 THEN BREAK 
2030 DATA 101, "abc"
2031 DATA 103
2032 READ X : IF X <> 101 THEN BREAK 
2034 READ A$ : IF A$ <> "abc" THEN BREAK 
2036 READ X : IF X <> 103 THEN BREAK 
2038 RESTORE : READ X : IF X <> 101 THEN BREAK 
2039 RESTORE 2031 : READ X : IF X <> 103 THEN BREAK 
2040 DEF FNDB(X) = X * 2 : IF FNDB(21) <> 42 THEN BREAK 
2050 DIM X(15) : X(15) = 42 : IF X(5) <> 0 OR X(15) <> 42 THEN BREAK 
2060 X = 1 : FOR I = 5 TO 1 STEP -1 : X = X * I : NEXT 
2062 IF X <> 120 THEN BREAK 
2070 X = 6 : Y = 7 : GOSUB 2900 : IF XY <> 42 THEN BREAK 
2080 HTAB 30 : IF POS(0) <> 30 THEN BREAK 
2090 V = POS(1) : VTAB 10 : IF POS(1) <> 10 THEN BREAK 
2091 VTAB V : HTAB 1
2100 IF 1 THEN X = 1 ELSE X = 0
2101 IF X <> 1 THEN BREAK 
2110 ON 1 + 2.9 GOTO 2111, 2112, 2113, 2114
2111 X = 111 : GOTO 2119
2112 X = 222 : GOTO 2119
2113 X = 333 : GOTO 2119
2114 X = 444 : GOTO 2119
2119 IF X <> 333 THEN BREAK 
2120 T0A = TIME(0) : T1A = TIME(1)
2122 WAIT 0.5
2124 T0B = TIME(0) : T1B = TIME(1)
2126 IF ABS((T0B - T0A) - 0.5) > 0.1 THEN BREAK 
2128 IF ABS((T1B - T1A) - 0.5) > 0.1 THEN BREAK 
2899 GOTO 2999
2900 REM Test Subroutine
2910 XY = X * Y
2920 RETURN 
2999 PRINT "General commands OK."
3000 REM Graphics Functions/Commands
3010 PRINT "Testing graphics functions/commands..."
3020 COLOR 4 : REM violet (#CC44CC)
3030 PLOT 0,0: IF PIXEL(0,0) <> 4 THEN BREAK
3032 IF PIXEL(0,1) <> 0 THEN BREAK
3040 ELLIPSE 100,100 TO 150,150
3042 IF PIXEL(125,100) <> 4 THEN BREAK
3043 IF PIXEL(125,125) <> 0 THEN BREAK
3050 FILL ON: ELLIPSE 100,100 TO 150,150
3052 IF PIXEL(125,125) <> 4 THEN BREAK
3054 IF PIXEL(100,100) <> 0 THEN BREAK
3060 RECT 100,100 TO 150,150
3064 IF PIXEL(100,100) <> 4 THEN BREAK
3098 COLOR 0: FILL ON: RECT 0, 0 TO 960, 640
3099 COLOR 5 : REM default green
4000 REM File I/O
9990 PRINT : PRINT "All tests complete."