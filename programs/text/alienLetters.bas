1  REM  ***********************
2  REM  *   ATTACK  OF  THE   *
3  REM  *    ALIEN  LETTERS   *
4  REM  *         BY          *
5  REM  *      JOE  STROUT    *
6  REM  *                     *
7  REM  *    COPYRIGHT (c)    *
8  REM  * 1985 MICROSPARC,INC *
9  REM  ***********************
10  TEXT : HOME : VTAB 23: PRINT "========================================";: REM  40 ='S
20  VTAB 21: HTAB 20: PRINT "!": HTAB 19: PRINT "(O)"
30 LT$ = "ASDFJKL;":NL = 3: REM  LETTERS USED, NO. OF LETTERS FALLING AT ONCE
40  GOSUB 500
50  FOR I = 1 TO NL - 1:L$(I) =  MID$ (LT$,I,1):LX(I) =  INT (40 *  RND (1) + 1):LY(I) = 1: GOSUB 530: NEXT : GOSUB 530
60  DEF  FN X(Q) =  SGN (LX(I) - BX): DEF  FN Y(Q) =  SGN (LY(I) - BY)
100  REM  GET NEW LETTER
110 L$(I) =  MID$ (LT$, INT ( LEN (LT$) *  RND (1) + 1),1):LX(I) =  INT (39 *  RND (1) + 1):LY(I) = 1 +  INT (SC / 15): IF LY(I) > 15 THEN LY(I) = 15
120 SC = SC + 1: VTAB 24: HTAB 29: PRINT "LETTERS:"SC;
150  REM  MAIN LOOP
160  FOR ML = 1 TO NL: GOSUB 300
170  IF  PEEK ( - 16384) < 128 THEN  NEXT : GOTO 160
180  REM  SHOOT
190 K$ =  CHR$ ( PEEK ( - 16384) - 128): POKE  - 16368,0: FOR I = 1 TO NL: IF K$ = L$(I) THEN CH$ = "^": GOSUB 230:CH$ = " ": GOSUB 230: GOTO 100
200  NEXT : IF K$ =  CHR$ (27) THEN  VTAB 12: HTAB 5: PRINT "PRESS ANY KEY TO CONTINUE:";: GET Q$: HTAB 5: PRINT "                            ";
210  GOSUB 530: NEXT : GOTO 160
230 BX = 20:BY = 20
240  HTAB BX: VTAB BY: PRINT CH$: IF BY = LY(I) AND BX = LX(I) THEN  RETURN 
250 BX = BX +  FN X(Q):BY = BY +  FN Y(Q): IF CH$ <  > " " THEN  POKE 768,BY * 4: POKE 769,2: CALL SOUND
260  GOTO 240
300  REM  MOVE LETTER ML
310  HTAB LX(ML): VTAB LY(ML): PRINT " ": HTAB LX(ML):LY(ML) = LY(ML) + 1: VTAB LY(ML): PRINT L$(ML)
320  IF LY(ML) <  > 22 THEN  RETURN 
330  GOSUB 530: GOSUB 530: VTAB 12: HTAB 9: PRINT "THE ALIENS HAVE LANDED.": HTAB 5: PRINT "BUT HEY, KEEP TRYING AND YOU CAN": HTAB 11: PRINT "LICK THOSE SUCKERS!"
340  VTAB 16: HTAB 6: PRINT "PRESS <RETURN> TO TRY AGAIN:";: GET A$: IF A$ <  >  CHR$ (13) THEN 340
350  CLEAR : GOTO 10
500  REM  INIT SOUND
510  RESTORE : FOR I = 770 TO 790: READ A: POKE I,A: NEXT :SOUND = 770: RETURN 
520  DATA 173,48,192,136,208,5,206,1,3,240,9,202,208,245,174,0,3,76,2,3,96
530  REM  BUZZ SOUND
540  FOR ZI = 1 TO 15: POKE 768,200: POKE 769,2: CALL SOUND: NEXT : RETURN 
