1  REM  ***********************
2  REM  *   ATTACK  OF  THE   *
3  REM  *    ALIEN  LETTERS   *
4  REM  *         BY          *
5  REM  *      JOE  STROUT    *
6  REM  *                     *
7  REM  *    COPYRIGHT (c)    *
8  REM  * 1985 MICROSPARC,INC *
9  REM  ***********************
10  HOME : VTAB 23: PRINT "========================================";: REM  40 ='S
20  VTAB 21: HTAB 20: PRINT "!": HTAB 19: PRINT "(O)"
30 LT$ = "ASDFJKL;":NL = 3: REM  LETTERS USED, NO. OF LETTERS FALLING AT ONCE
50  FOR I = 1 TO NL - 1:L$(I) =  MID$ (LT$,I,1):LX(I) =  INT (40 *  RND (1) + 1):LY(I) = 1: NEXT
60  DEF  FN X(Q) =  SGN (LX(I) - BX): DEF  FN Y(Q) =  SGN (LY(I) - BY)
100  REM  GET NEW LETTER
110 L$(I) =  MID$ (LT$, INT ( LEN (LT$) *  RND (1) + 1),1):LX(I) =  INT (39 *  RND (1) + 1):LY(I) = 1 +  INT (SC / 15): IF LY(I) > 15 THEN LY(I) = 15
115 GOSUB 530
120 SC = SC + 1: VTAB 24: HTAB 29: PRINT "LETTERS:"SC;
150  REM  MAIN LOOP
160  FOR ML = 1 TO NL: GOSUB 300
170  K$ = INKEY$(0): IF K$ = "" THEN NEXT : WAIT 0.1 : GOTO 160
180  REM  SHOOT
185 K$ = UPPER$(K$)
190 FOR I = 1 TO NL: IF K$ = L$(I) THEN CH$ = "^": GOSUB 220:CH$ = " ": GOSUB 220: GOTO 100
200  NEXT : IF K$ =  CHR$ (27) THEN  VTAB 12: HTAB 5: PRINT "PRESS ANY KEY TO CONTINUE:";: GET Q$: HTAB 5: PRINT "                            ";
210  GOSUB 530: NEXT : GOTO 160
220 REM DRAW SHOT TO LETTER I
230 BX = 20:BY = 20
240  HTAB BX: VTAB BY: PRINT CH$: IF BY = LY(I) AND BX = LX(I) THEN  RETURN 
250 BX = BX +  FN X(Q):BY = BY +  FN Y(Q): IF CH$ <  > " " THEN GOSUB 510
260  GOTO 240
300  REM  MOVE LETTER ML
310  HTAB LX(ML): VTAB LY(ML): PRINT " ": HTAB LX(ML):LY(ML) = LY(ML) + 1: VTAB LY(ML): PRINT L$(ML)
320  IF LY(ML) <  > 22 THEN  RETURN 
330  GOSUB 550: VTAB 12: HTAB 9: PRINT "THE ALIENS HAVE LANDED.": HTAB 5: PRINT "BUT HEY, KEEP TRYING AND YOU CAN": HTAB 11: PRINT "LICK THOSE SUCKERS!"
340  VTAB 16: HTAB 6: PRINT "PRESS <RETURN> TO TRY AGAIN:";: GET A$
350  IF A$ = CHR$(27) THEN PRINT: VTAB 26: END
360  IF A$ <  >  CHR$ (10) THEN 340
370  CLEAR : GOTO 10
510 REM  SHOT SOUND
520  REDIM F(1): F(0)= -24 - 2*POS(1): F(1)=F(0)/2: SOUND F(), 0.1, 0.1, 3
525  RETURN
530 REM  ALIEN APPEARS SOUND
540  REDIM V(1): V(0)=1: V(1)=0: SOUND 1, 0.1, V(), 4
545  RETURN
550 REM  CRASH SOUND
560  REDIM V(1): V(0)=1: V(1)=0: SOUND 0.5, 2, V(), 4
565  RETURN
