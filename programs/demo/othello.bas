10  REM OTHELLO
11  REM By Joe Strout (1988 or so)
20  DIM B(9,9),C(9,9),SX(64),SY(64)
30 B(4,4) = 1:B(5,5) = 1:B(4,5) = 2:B(5,4) = 2
35 DS$ = ".XO":DS = 1
40  HOME : PRINT : PRINT : PRINT 
100  HTAB 17: PRINT "OTHELLO": PRINT : PRINT : HTAB 13: PRINT "By Joe Strout": HTAB 8: REM PRINT "Dedicated to Lisa Schumm"
110  PRINT : PRINT : PRINT "How many players (0-2) ? ";
120  GET A$: IF A$ < "0" OR A$ > "2" THEN 120
130  PRINT A$:NP =  VAL (A$)
140  IF NP = 2 THEN 1300
150  IF  NOT NP THEN 1500
160  PRINT : PRINT "Select which to play, X or O : ";
170  GET A$: IF A$ <  > "X" AND A$ <  > "O" THEN 170
180 PC = 1 + (A$ = "O"):CC = 1 + (A$ = "X")
190  GOTO 1000
700  REM  FLIP ALL TILES RESULTING FROM X,Y
710 ST = 1:SP = 1:SX(1) = X:SY(1) = Y
720 X = SX(SP):Y = SY(SP): GOSUB 750:SP = SP + 1: IF SP <= ST THEN 720
730 X = SX(1):Y = SY(1): RETURN 
750  REM  FLIP TILES ADJACENT TO X,Y
760  FOR DX =  - 1 TO 1: FOR DY =  - 1 TO 1: GOSUB 800: NEXT DY,DX: RETURN 
800  REM  FLIP TILES FROM X, Y IN DIR. DX,DY
810 I = 1:F = 0:C = B(X,Y): IF  NOT DX AND  NOT DY THEN  RETURN 
820 A = B(X + DX * I,Y + DY * I): IF A = 0 OR (A = C AND F = 0) THEN  RETURN 
830  IF A <  > C THEN F = 1:I = I + 1: GOTO 820
840  IF A = C THEN  FOR J = 1 TO I - 1:B(X + DX * J,Y + DY * J) = C:ST = ST + 1:SX(ST) = X + DX * J:SY(ST) = Y + DY * J
850  IF DS THEN  VTAB 3 + Y + DY * J: HTAB 15 + X + DX * J: PRINT  MID$ (DS$,C + 1,1):SOUND 1, 0.02, 1, 4
860  NEXT : RETURN 
900  REM  PRINT BOARD
905 H = 15 + 1 : VTAB 3 : HTAB H : PRINT "ABCDEFGH"
910  FOR Y = 1 TO 8: HTAB H - 1: PRINT Y;: FOR X = 1 TO 8: PRINT  MID$ (DS$,B(X,Y) + 1,1);: NEXT : PRINT Y: NEXT : HTAB H: PRINT "ABCDEFGH": RETURN 
1000  REM   NORMAL - ONE PLAYER
1010  HOME : HTAB 17: PRINT "OTHELLO"
1015  IF PC = 2 THEN 1190
1020  VTAB 4: HTAB 16: GOSUB 900: GOSUB 6000: PRINT : PRINT 
1030 X = 0:Y = 0
1100  PRINT "Your turn (" MID$ ("•XO",PC + 1,1)")": PRINT : PRINT "Enter your choice,": PRINT "or <RETURN> to pass: ";
1110  GOSUB 3000: IF X =  - 1 THEN 1020
1190  VTAB 4: HTAB 16: GOSUB 900: GOSUB 6000: PRINT : PRINT 
1195  IF PA > 1 THEN 2000
1200  PRINT "My turn (" MID$ ("•XO",CC + 1,1)")": PRINT : PRINT "    Don't rush me...";
1210  GOSUB 5000: IF PA < 2 THEN 1020
1220  GOTO 2000
1300  REM   TWO PLAYERS
1310  HOME : HTAB 17: PRINT "OTHELLO"
1320  FOR P = 1 TO 2
1330  VTAB 4: HTAB 16: GOSUB 900: GOSUB 6000: PRINT : PRINT :X = 0:Y = 0
1335  IF P = 2 THEN  PRINT 
1340  PRINT "Player "P" (" MID$ (DS$,P + 1,1)"), enter your choice,": PRINT "  or press <RETURN> to pass: ";
1350 PC = P: GOSUB 3000: IF X =  - 1 THEN 1330
1360  IF PA < 2 THEN  NEXT P: GOTO 1320
1400  HOME : HTAB 17: PRINT "Othello": VTAB 4: HTAB 16: GOSUB 900
1410  PRINT :A$ = "<<< GAME OVER >>>": HTAB 20 -  LEN (A$) / 2: PRINT A$
1430  PRINT :P1 = 0:P2 = 0: FOR X = 1 TO 8: FOR Y = 1 TO 8:P1 = P1 + (B(X,Y) = 1):P2 = P2 + (B(X,Y) = 2): NEXT Y,X
1440  PRINT "Player One's score: "P1: PRINT "Player Two's score: "P2
1450  PRINT : IF P1 > P2 THEN  PRINT "   Player One wins!!!": GOTO 2100
1460  IF P2 > P1 THEN  PRINT "   Player Two wins!!!": GOTO 2100
1470  PRINT "   A tie!": GOTO 2100
1500  REM  ZERO PLAYERS
1510  HOME : HTAB 17: PRINT "OTHELLO"
1520  FOR P = 1 TO 2
1530  VTAB 4: HTAB 16: GOSUB 900: GOSUB 6000: PRINT : PRINT :X = 0:Y = 0
1535  IF P = 2 THEN  PRINT 
1540  PRINT "Player "P" is thinking...";:CC = P: GOSUB 5000
1550  IF PA < 2 THEN  NEXT P: GOTO 1520
1560  GOTO 1400
2000  REM   GAME OVER
2010  HOME : HTAB 17: PRINT "OTHELLO": VTAB 4: HTAB 16: GOSUB 900
2020  PRINT :A$ = "<<< GAME OVER >>>": HTAB 20 -  LEN (A$) / 2: PRINT A$
2030  PRINT :PS = 0:CS = 0: FOR X = 1 TO 8: FOR Y = 1 TO 8:PS = PS + (B(X,Y) = PC):CS = CS + (B(X,Y) = CC): NEXT Y,X
2040  PRINT "Your score: "PS: PRINT "  My score: "CS
2050  PRINT : IF CS > PS THEN  PRINT "   I win!": GOTO 2100
2060  IF PS > CS THEN  PRINT "   You win!!!": GOTO 2100
2070  PRINT "   A tie!"
2100  PRINT : PRINT "Would you like to play again? ";
2110  GET A$: IF A$ <  > "Y" AND A$ <  > "N" THEN 2110
2120  IF A$ = "Y" THEN  PRINT "Yes": FOR J = 1 TO 1000: NEXT : RUN 
2130  PRINT "No": PRINT : PRINT "Thanks for playing!": END 
3000  REM  PLAYER'S TURN
3010  GET A$: IF A$ =  CHR$ (13) THEN PA = PA + 1: RETURN
3020  IF  VAL (A$) THEN  PRINT A$;:Y =  VAL (A$): IF  NOT X THEN  PRINT "-";: GOTO 3010
3030  IF  VAL (A$) THEN 3050
3040  A$ = UPPER$(A$): IF A$ < "A" OR A$ > "H" THEN 3010
3045  PRINT A$;:X =  ASC (A$) - 64: IF  NOT Y THEN  PRINT "-";: GOTO 3010
3050  PRINT : PRINT 
3060  IF B(X,Y) THEN  PRINT "Illegal move (there's already a piece": PRINT "in that spot!)": FOR J = 1 TO 2000: NEXT : VTAB  POS(1) - 3:X =  - 1: RETURN 
3070  VTAB 3 + Y: HTAB 15 + X: PRINT  MID$ (DS$,PC + 1,1);:SOUND 1, 0.02, 1, 4
3080 B(X,Y) = PC: GOSUB 700: IF ST = 1 THEN  VTAB 20: PRINT : PRINT "Illegal move (you must flip some tiles.":B(X,Y) = 0:X =  - 1: RETURN 
3090 PA = 0: RETURN 
5000  REM   COMPUTER'S TURN
5010  FOR X = 1 TO 8: FOR Y = 1 TO 8:C(X,Y) = B(X,Y): NEXT Y,X:BS = 0:DS = 0
5020  FOR X = 1 TO 8: FOR Y = 1 TO 8: IF B(X,Y) THEN 5190
5030 B(X,Y) = CC: GOSUB 700: IF ST = 1 THEN B(X,Y) = 0: GOTO 5190
5040 ST = ST + ((X = 1 OR X = 8) + (Y = 1 OR Y = 8)) ^ 2: IF ST +  INT ( RND (1) * 2) > BS THEN BS = ST:BX = X:BY = Y
5050  FOR I = 1 TO 8: FOR J = 1 TO 8:B(I,J) = C(I,J): NEXT J,I
5190  NEXT Y: PRINT ".";: NEXT X: PRINT 
5200  REM  DECIDED
5205  IF  NOT BS THEN  PRINT : PRINT "CAN'T MOVE.": FOR J = 1 TO 1000: NEXT :PA = PA + 1:DS = 1: RETURN 
5210  VTAB 3 + BY: HTAB 15 + BX: PRINT  MID$ ("XO",CC + 1,1);:SOUND 1, 0.02, 1, 4
5215 PA = 0:DS = 1
5220  FOR X = 1 TO 8: FOR Y = 1 TO 8:B(X,Y) = C(X,Y): NEXT Y,X:B(BX,BY) = CC:X = BX:Y = BY: GOTO 700
6000 REM  Clear to the bottom of the text screen
6010 V = POS(1): HTAB 1
6020 PRINT SPC(67)
6030 IF POS(1) < 26 THEN 6020
6040 VTAB V
6050 RETURN